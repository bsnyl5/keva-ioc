plugins {
    id 'idea'
    id 'java-library'
    id 'application'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

group 'dev.keva'
version '0.1.0-SNAPSHOT'

configurations {
    extraLibs
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    // SLF4J as a facade
    implementation 'org.slf4j:slf4j-api:1.7.2'

    implementation 'org.reflections:reflections:0.10.1'
    extraLibs 'org.reflections:reflections:0.10.1'

    // JUnit
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

application {
    mainClassName = 'dev.keva.ioc.Main'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("sonatypeUsername") ?: System.getenv("sonatypeUsernamer")
                password = project.findProperty("sonatypePassword") ?: System.getenv("sonatypePassword")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'keva-ioc'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Keva IoC'
                description = 'Fast, lightweight, Spring like, annotation driven IoC framework'
                url = 'https://ioc.keva.dev'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/keva-dev/keva-ioc/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'tuhuynh27'
                        name = 'Tu Huynh'
                        email = 'huynhminhtufu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/keva-dev/keva-ioc.git'
                    developerConnection = 'scm:git:ssh://github.com:keva-dev/keva-ioc.git'
                    url = 'https://github.com/keva-dev/keva-ioc'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
